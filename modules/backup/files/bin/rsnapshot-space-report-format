#!/usr/bin/perl -w
#
# Author:	Paul Gear
# Created:	2007-10-03
# Description:	Take du output of an rsnapshot backup directory and display it nicely.
# Usage:	See rsnapshot-space-report
#

use strict;
use CGI::Pretty qw/:html/;
use File::Basename;

my %detail;
my $summary = ' SUMMARY ';
my $subtotal = ' SUBTOTAL ';
my @total;

# read the input into our hash
while (<STDIN>) {
    chomp;

    my @F = split /\//;
    my @T = split;

    if ($#F == 2) {
	# detail line
	$detail{$F[2]}->{$F[1]} = $T[0] / 1024;
	$detail{$F[2]}->{$subtotal} += $T[0] / 1024;
    }
    elsif ($#F == 1) {
	# summary line
	$detail{$summary}->{$F[1]} = $T[0] / 1024;
    }
    else {
	# total line
	push @total, $T[0] / 1024;
    }
}

if (-t 1) {
    # If sending output to the screen, just display plain text

    my $hosts_header = "%-15s %-10s %12s\n";
    my $hosts_format = "%-15s %-10s %12d\n";
    printf $hosts_header, "Host", "Snapshot", "MB Used";
    printf $hosts_header, "----", "--------", "-------";

    for my $host (sort keys %detail) {
	my $tmphost = $host;
	for my $snapshot (sort keys %{$detail{$host}}) {
	    next if $host eq $summary;
	    printf $hosts_format, $tmphost, $snapshot, $detail{$host}->{$snapshot};
	    $tmphost = "";
	}
    }

    my $summary_header = "%-10s %12s\n";
    my $summary_format = "%-10s %12d\n";

    printf "\n";
    printf $summary_header, "Snapshot", "GB Used";
    printf $summary_header, "--------", "-------";

    for my $period (sort keys %{$detail{$summary}}) {
	printf $summary_format, $period, $detail{$summary}->{$period} / 1024;
    }

    for my $total (sort @total) {
	printf $summary_format, "TOTAL", $total / 1024;
    }
}
else {
    # Otherwise, produce HTML output.

    my $dir = $ARGV[0];
    my $title = "Rsnapshot Space Report: $dir";

    print h1($dir);

    my $base = basename $dir;
    print p(img {src=>"$base/rsnapgraph-files.png"});
    print p(img {src=>"$base/rsnapgraph-size.png"});

    print "<table border=1 cellspacing=0>\n";
    print Tr(th(['Host', 'Snapshot', 'MB Used']));

    for my $host (sort keys %detail) {
	next if $host eq $summary;
	my $tmphost = $host;
	for my $snapshot (sort keys %{$detail{$host}}) {
	    next if $snapshot eq $subtotal;
	    print Tr(td(["$tmphost", "$snapshot"]),
		td({-align=>'right'},[sprintf "%d", "$detail{$host}->{$snapshot}"]));
	    $tmphost = "";
	}
	print Tr(td([b("$tmphost"), b("$subtotal")]),
		td({-align=>'right'},b(sprintf "%d", "$detail{$host}->{$subtotal}")));
    }
    print "</table>\n";

    print "<table border=1 cellspacing=0>";
    print Tr(th(["Snapshot", "GB Used"]));
    for my $period (sort keys %{$detail{$summary}}) {
	print Tr(td("$period"), td({-align=>'right'}, sprintf "%d", $detail{$summary}->{$period} / 1024));
    }
    for my $total (sort @total) {
	print Tr(td(b("TOTAL")), td({-align=>'right'}, b(sprintf "%d", $total / 1024)));
    }
    print "</table>\n";
}
