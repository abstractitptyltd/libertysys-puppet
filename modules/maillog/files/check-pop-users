#!/usr/bin/perl -Tw
#
# Author:	Paul Gear
# Created:	2010-06-04
# Modified:	2010-06-05
# Description:	Parses syslog lines like this into POP statistics:
#		Jun  4 22:57:58 linserv dovecot: POP3(cpassisacc): Disconnected: Logged out top=0/0, retr=1/2454, del=1/1, size=2437
#

use strict;

my ($connections, %users, %messages, %sizes);

if ($#ARGV < 0) {
	push @ARGV, "/var/log/maillog";
}

my $i = 0;
my $start;
$connections = 0;

while (<>) {
	# find the date at which the log begins
	if ($i == 0) {
		($start) = /^(\S+\s+\d+\s+[\d:]+)\s/;
	}
	++$i;
	
	# skip everything except the lines we're interested in
	next unless /dovecot: POP3.*: Disconnected:/;

	# parse the fields
	my ($user, $retr, $size) = /dovecot: POP3\((.*)\): Disconnected: Logged out top=.* retr=(\d+)\/\d+, del=\d+\/\d+, size=(\d+)/;

	++$connections;
	++$users{$user};
	$messages{$user} += $retr;
	$sizes{$user} += $size;
}

printf "%d POP logins since %s\n", $connections, $start;
if ($connections > 0) {
	printf "%-25s %12s %10s %10s %13s\n", "User", "Connections", "Messages", "Size (Kb)", "Average (Kb)";
	printf "%-25s %12s %10s %10s %13s\n", "----", "-----------", "--------", "---------", "------------";
	for my $user (sort keys %users) {
		printf "%-25s %12d %10d %10d %13d\n", $user, $users{$user}, $messages{$user}, $sizes{$user} / 1024, $messages{$user} > 0 ? $sizes{$user} / 1024 / $messages{$user} : -1;
	}
}
